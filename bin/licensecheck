#!/usr/bin/perl

use strict;
use warnings;

my $copyright = <<EOF;

%%
%  Copyright 2011 Jacek B. Krawczyk and Alastair Pharo
%
%  Licensed under the Apache License, Version 2.0 (the "License");
%  you may not use this file except in compliance with the License.
%  You may obtain a copy of the License at
%
%      http://www.apache.org/licenses/LICENSE-2.0
%
%  Unless required by applicable law or agreed to in writing, software
%  distributed under the License is distributed on an "AS IS" BASIS,
%  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
%  See the License for the specific language governing permissions and
%  limitations under the License.
EOF

my $oldcopyright = qr/^\%+\s+\QCopyright (C) 2011 by Jacek B. Krawczyk and Alastair Pharo\E/;
my $newcopyright = qr/^\%+\s+\QCopyright 2011 Jacek B. Krawczyk and Alastair Pharo\E/;
my $license = qr/^\%+\s+\QLicensed under the Apache License, Version 2.0\E/;

my @files = @ARGV;

# Loop through all files in our list.
for my $filename (@files) {
  open my $file, '+<', $filename;

  # Look for the old copyright message.
  my ($foundold, $foundnew, $foundlicense) = (0, 0, 0);
  my $curpos = tell($file);
  while (<$file>) {
    $foundold = 1, last
      if $_ =~ $oldcopyright;

    $foundnew = 1
      if $_ =~ $newcopyright;

    $foundlicense = 1
      if $_ =~ $license;

    $curpos = tell($file);
  }

  if ($foundold) {
    # If we found it, get the rest of the file, not including the old copyright.
    my $rest = '';
    my $truncpos = $curpos;
    while (<$file>) {
      $rest .= $_;
    }

    # Dump the end of the file.
    truncate $file, $truncpos;
    
    # Seek to the new end
    seek $file, 0, 2;

    # Append the new copyright.
    print $file $copyright;

    # Append the rest of the file.
    print $file $rest;
  }
  elsif (!$foundnew) {
    warn "No copyright in $filename\n"
      unless $filename =~ /Contents\.m/;
  }
  elsif (!$foundlicense) {
    warn "No license in $filename\n"
      unless $filename =~ /Contents\.m/;
  }

  # Save the result.
  close $file;
}
